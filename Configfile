LANGUAGES   += c
COMPILEOPTS += -Wall
COMPILEOPTS += -Wextra
COMPILEOPTS += -Werror
COMPILEOPTS += -Wno-unused-parameter

LANGUAGES   += perl

LANGUAGES   += bash

GENERATE    += version.h

BINARIES    += tek
#TESTDEPS    += bin/texpp
#TESTDEPS    += bin/texpp-html
#TESTDEPS    += bin/gnuplotpp
#TESTDEPS    += bin/texstrip
#TESTDEPS    += bin/svgtexpp
LINKOPTS    += `ppkg-config --optional --have TALLOC talloc --libs`
COMPILEOPTS += `ppkg-config --optional --have TALLOC talloc --cflags`
COMPILEOPTS += -Isrc/extern/
SOURCES     += tek/main.c
TESTSRC     += input-stex.bash
TESTSRC     += input-tex.bash
TESTSRC     += input-noext.bash
TESTSRC     += includegraphics-in-comment.bash
TESTSRC     += local-cls.bash
TESTSRC     += lulu-subchapter.bash
TESTSRC     += lulu-pdfcrop-in-subchapter.bash
TESTSRC     += image.bash
TESTSRC     += palmer-master-thesis.bash

BINARIES    += texpp
SOURCES     += texpp/main.c
TESTSRC     += input-stex.bash
TESTSRC     += input-tex.bash
TESTSRC     += input-noext.bash
TESTSRC     += includegraphics-in-comment.bash

BINARIES    += texpp-html
SOURCES     += texpp-html/main.c

BINARIES    += gnuplotpp
SOURCES     += gnuplotpp/main.c

BINARIES    += texstrip
SOURCES     += texstrip/main.c
TESTSRC     += input-stex.bash
TESTSRC     += input-tex.bash
TESTSRC     += input-noext.bash

BINARIES    += svgtexpp
SOURCES     += svgtexpp.pl
